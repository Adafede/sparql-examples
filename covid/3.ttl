prefix ex: <https://covid-19-sparql.expasy.org/.well-known/sparql-examples/> 
prefix up: <http://purl.uniprot.org/core/> 
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#> 

ex:3
    a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
    sh:prefixes ex: ;
    rdfs:comment """For the exons annotated in the INDSC records show the nodes in the pangenome graph that overlap""" ;
    sh:select """SELECT 
    DISTINCT 
        ?node ?annotation ?sbpValue ?bpValue ?sepValue ?epValue
WHERE
{ 
  ?path a vg:Path .
  ?uniprot rdfs:seeAlso ?emblcds . 
  ?emblcds up:locatedOn/owl:sameAs ?path .
  ?step a vg:Step ;
          vg:path ?path ;
          vg:node ?node ;
          faldo:begin/faldo:position ?sbpValue ;
          faldo:end/faldo:position ?sepValue .

  ?region faldo:begin ?bp ;
          faldo:end ?ep .
  ?annotation faldo:location ?region ;
           a insdcschema:Exon .
  ?bp faldo:reference ?p ;
          faldo:position ?bpValue .
  ?ep faldo:reference ?p ;
          faldo:position ?epValue .
  MINUS {?region a vg:Step }
  FILTER((?bpValue <= ?sbpValue) && (?epValue >= ?sepValue))
}""" .
