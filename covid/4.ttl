prefix ex: <https://covid-19-sparql.expasy.org/.well-known/sparql-examples/> 
prefix up: <http://purl.uniprot.org/core/> 
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#> 

ex:4
    a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
    sh:prefixes ex: ;
    rdfs:comment """Find annotations that are annotated in CDS that differ by one in pangenome node space from a other CDS on the places where these mismatch.""" ;
    sh:select """SELECT 
	DISTINCT 
		?insdCDS #?insdCDSBegin ?insdCDSEnd ?step ?path ?node ?nextNode ?nextPath ?insdcStepBegin ?insdcStepEnd ?uniprotSequence
		?uniprot ?stepBeginInProteinSpace ?stepEndInProteinSpace ?annotationText
WHERE
{
#  Find CDS annoted by INDSC that do not match a UniProt protein.
  ?insdCDS insdc:translation ?sequence ;
           a insdc:Coding_Sequence ;
           faldo:location ?insdCDSLocation .
  MINUS {
  	?uniprotSequence rdf:value ?sequence .
  }
  
#   Get the range of this CDS and make sure the coordinates are on the 
#  path we need later
  ?insdCDSLocation faldo:begin [ faldo:reference ?path ;
                                 faldo:position ?insdCDSBegin] ;
                   faldo:end [ faldo:reference ?path ;
                                 faldo:position ?insdCDSEnd] .
  
  ?step a vg:Step ;
  	vg:path ?path ;
    vg:node ?node ;
  	faldo:begin [ faldo:reference ?path ;
      				faldo:position ?insdcStepBegin ] ;
  	faldo:end [ faldo:reference ?path ;
            		faldo:position ?insdcStepEnd ] .
# I always forget how to interval ranges :(
  FILTER ( (?insdcStepBegin >= ?insdCDSBegin && ?insdcStepBegin <= ?insdCDSEnd) || 
    	   (?insdCDSBegin >= ?insdcStepBegin && ?insdCDSBegin <= ?insdcStepEnd) || 
   		   (?insdcStepEnd >= ?insdCDSEnd && ?insdcStepEnd <= ?insdCDSBegin) || 
    	   (?insdCDSEnd >= ?insdcStepEnd && ?insdCDSEnd <= ?insdcStepBegin) )
#  Then we look for a node close to the ones in the CDS in genome graph space (one step)
  ?node vg:linksForwardToForward ?nextNode . 
  ?step2 a vg:Step ;
  	vg:path ?nextPath ;
    vg:node ?nextNode .
#  Where that node is on a uniprot matching sequence
  ?nextinsdCDS insdc:translation ?nextSequence ;
           a insdc:Coding_Sequence ;
           faldo:location/faldo:begin/faldo:reference ?nextPath .
  ?uniprot up:sequence/rdf:value ?nextSequence . 

  BIND(IF(?insdCDSBegin > ?insdcStepBegin, ?insdCDSBegin, ?insdcStepBegin - ?insdCDSBegin)/3 AS ?stepBeginInProteinSpace)
  BIND(IF(?insdCDSEnd > ?insdcStepEnd, ?insdcStepEnd, ?insdCDSBegin - ?insdcStepEnd)/3 AS ?stepEndInProteinSpace)
  ?uniprot up:annotation ?annotation .
  ?annotation up:range ?annotationRegion .
  ?annotation rdfs:comment ?annotationText .
  ?annotationRegion faldo:begin/faldo:position ?annotationBegin .
  ?annotationRegion faldo:end/faldo:position ?annotationEnd .
  FILTER (?annotationBegin >= ?stepBeginInProteinSpace && ?annotationEnd < ?stepEndInProteinSpace )
}""" .
