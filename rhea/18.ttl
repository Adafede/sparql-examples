prefix ex: <https://sparql.rhea-db.org/.well-known/sparql-examples/> 
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#> 
ex:18
    a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
    sh:prefixes ex: ;
    rdfs:comment """Select all approved reactions with <a href="https://www.ebi.ac.uk/chebi/searchId.do?chebiId=CHEBI:17815">CHEBI:17815</a> (a 1,2-diacyl-sn-glycerol) or one of its descendant. Display the EC numbers if the rhea-ec link exists"""^^rdf:html ;
    sh:select """SELECT
  DISTINCT
    ?reaction 
    ?ec 
    ?reactionEquation 
WHERE {
  ?reaction rdfs:subClassOf rh:Reaction .
  ?reaction rh:status rh:Approved .
  ?reaction rh:equation ?reactionEquation .
  ?reaction rh:side ?reactionSide .

  OPTIONAL {?reaction rh:ec ?ec .} .

  ?reactionSide rh:contains ?participant .
  ?participant rh:compound ?compound .
  ?compound rh:chebi ?chebi .
  ?chebi rdfs:subClassOf* CHEBI:17815 .
}
ORDER BY ?reaction""" .
