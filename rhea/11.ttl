prefix ex: <https://sparql.rhea-db.org/.well-known/sparql-examples/> 
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#> 
ex:11
    a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
    sh:prefixes ex: ;
    rdfs:comment """Select all compounds and count their occurence in Rhea reactions"""^^rdf:html ;
    sh:select """SELECT
  ?compoundAc 
  ?chebi
  (COUNT(DISTINCT ?reaction) AS ?reactionCount)
  ?compoundName 
WHERE {
  ?reaction rdfs:subClassOf rh:Reaction .
  ?reaction rh:status rh:Approved .
  ?reaction rh:side ?reactionSide .
  ?reactionSide rh:contains ?participant .
  ?participant rh:compound ?compound .
  OPTIONAL { ?compound rh:chebi ?chebi . }
  ?compound rh:name ?compoundName .
  ?compound rh:accession ?compoundAc .
}
GROUP BY ?compoundAc ?chebi ?compoundName
ORDER BY DESC(?reactionCount)""" .

