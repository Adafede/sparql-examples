prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<https://schema.org/>
ex:NXQ_00025
schema:target <https://sparql.nextprot.org/sparql> ;
 sh:prefixes _:sparql_examples_prefixes ;
  a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
  rdfs:comment '''Proteins with at least 50 interactors that are not associated with a disease''' ;
 schema:keyword "PPI";
 schema:keyword "disease";
 schema:keyword "tutorial";
  sh:select '''select distinct ?entry where {
 ?entry :isoform ?iso.
 ?iso :binaryInteraction/:interactant ?interactant.
 
 # with no disease
 filter not exists {
 {
 ?iso :medical / rdf:type :Disease.
 } union {
 ?iso :uniprotKeyword / :term ?kw .
 ?kw :termType \"Disease\"^^xsd:string
 filter (?kw != nextprot_cv:KW-0656)
 }
 }

} group by ?entry ?iso having (count(?interactant) >= 50)'''
.
